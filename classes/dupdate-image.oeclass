IMAGE_BASENAME ?= "${MACHINE}-${PN}"
IMAGE_FULLNAME ?= "${IMAGE_BASENAME}-${PV}-${DATETIME}"

inherit image

DUPDATE_IMAGE_EXT = '.dupdate'

IMAGE_CREATE_FUNCS += "dupdate_image"

dupdate_image() {
	(
	sha256sum * > sha256sum.txt
	tar chf ${B}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT} .
	)
}

CLASS_FLAGS += "dupdate_script dupdate_version"
DEFAULT_USE_dupdate_script = ""
DEFAULT_USE_dupdate_version = ""

do_install[postfuncs] += "do_install_dupdate_image"
do_install_dupdate_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT} ${D}/
}

POSTFUNCS:>USE_dupdate_version = " dupdate_version"
POSTFUNCS:>USE_dupdate_script = " dupdate_script"
POSTFUNCS += "dupdate_flatten_bootdir"
dupdate_flatten_bootdir () {
    if [ -d boot ]; then
	  mv boot/* .
	  rmdir boot
    fi
}
dupdate_flatten_bootdir[dirs] = "${RSTAGE_DIR}"

dupdate_version () {
	echo "${USE_dupdate_version}" > VERSION
}
dupdate_version[dirs] = "${RSTAGE_DIR}"

dupdate_script () {
	script="${USE_dupdate_script}"
	if [ "$script" != "run_update.sh" ] ; then
		mv $script run_update.sh
	fi
}
dupdate_script[dirs] = "${RSTAGE_DIR}"

do_rstage[postfuncs] += "${POSTFUNCS}"

do_deploy[postfuncs] += "do_deploy_dupdate_image"
do_deploy_dupdate_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT} \
		${IMAGE_DEPLOY_DIR}/${IMAGE_FULLNAME}${DUPDATE_IMAGE_EXT}
	ln -sf ${IMAGE_FULLNAME}${DUPDATE_IMAGE_EXT} \
		${IMAGE_DEPLOY_DIR}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT}
}

FILES_${PN} += "/${MACHINE}-${PN}_${DISTRO_VERSION}.dupdate"
