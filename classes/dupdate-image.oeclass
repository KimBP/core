## Used for creating dupdate tarballs containing multiple files.
##
## A dupdate file is a tarball containing: A VERSION, a runscript, a number of
## images, and a checksum of all the files.
##
## @useflag dupdate_script Set it to the name of dupdate script.
## @useflag dupdate_version Set it to the dupdate version (will be placed in
##          the VERSION file.
## @var IMAGE_BASENAME Set to a different basename.
## @var IMAGE_FULLNAME Set to a different fullname.

IMAGE_BASENAME ?= "${MACHINE}-${PN}"
IMAGE_FULLNAME ?= "${IMAGE_BASENAME}-${PV}-${DATETIME}"

inherit image

DUPDATE_IMAGE_EXT = '.dupdate'

IMAGE_CREATE_FUNCS += "dupdate_image"

dupdate_image() {
	(
	sha256sum * > sha256sum.txt
	tar chf ${B}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT} .
	)
}

CLASS_FLAGS += "dupdate_script dupdate_version"
DEFAULT_USE_dupdate_script = ""
DEFAULT_USE_dupdate_version = ""

CLASS_FLAGS += "dupdate_exefile"
require conf/dupdate.conf

do_install[postfuncs] += "do_install_dupdate_image"
do_install_dupdate_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT} ${D}/
}

do_rstage[postfuncs] += "${DUPDATE_RSTAGE_POSTFUNCS}"
DUPDATE_RSTAGE_POSTFUNCS = "do_rstage_dupdate_flatten_bootdir"
DUPDATE_RSTAGE_POSTFUNCS:>USE_dupdate_version = " do_rstage_dupdate_version"
DUPDATE_RSTAGE_POSTFUNCS:>USE_dupdate_script = " do_rstage_dupdate_script"

do_rstage_dupdate_flatten_bootdir () {
    if [ -d boot ]; then
	  mv boot/* .
	  rmdir boot
    fi
}
do_rstage_dupdate_flatten_bootdir[dirs] = "${RSTAGE_DIR}"

do_rstage_dupdate_version () {
	echo "${USE_dupdate_version}" > VERSION
}
do_rstage_dupdate_version[dirs] = "${RSTAGE_DIR}"

do_rstage_dupdate_script () {
	script="${USE_dupdate_script}"
	if [ "$script" != "${USE_dupdate_exefile}" ] ; then
		mv $script ${USE_dupdate_exefile}
	fi
}
do_rstage_dupdate_script[dirs] = "${RSTAGE_DIR}"

do_deploy[postfuncs] += "do_deploy_dupdate_image"
do_deploy_dupdate_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT} \
		${IMAGE_DEPLOY_DIR}/${IMAGE_FULLNAME}${DUPDATE_IMAGE_EXT}
	ln -sf ${IMAGE_FULLNAME}${DUPDATE_IMAGE_EXT} \
		${IMAGE_DEPLOY_DIR}/${IMAGE_BASENAME}${DUPDATE_IMAGE_EXT}
}

FILES_${PN} += "/${MACHINE}-${PN}_${DISTRO_VERSION}.dupdate"

# Local Variables:
# mode: python
# End:
