## OE-lite class for recipes providing content for inetd.conf file
##
## Inherit this class in recipes needing to add stuff to /etc/inetd .
## Recipes install task should install the required content into the
## /etc/inetd.conf file, and the fixup_inetd will relocate this to
## /etc/inetd.conf.d/${PN} which will the be merged with the (master)
## /etc/inetd.conf file together with other additions in the compile task
## of image builds. Set the USE flag inetd is you wish to use inetd.
##
## @useflag inetd Enable inetd by setting it.

require conf/inetd.conf

CLASS_FLAGS += "inetd"

RDEPENDS_INETD ?= "inetd"
RDEPENDS_${PN}:>USE_inetd = " ${RDEPENDS_INETD}"

INETD_CONF_FILES ?= "${SRCDIR}/inetd.conf"

do_install[postfuncs] += "${DO_INSTALL_INETD}"
DO_INSTALL_INETD:USE_inetd = "do_install_inetd"
do_install_inetd () {
    i=0
    for f in ${INETD_CONF_FILES} ; do
        if [ -f $f ] ; then
            # only create inetddir when needed, and let it fail silently when
            # called more than once
            mkdir -p ./${inetddir}
            let i=$i+1
            cp $f ./${inetddir}/${PN}.$i
        fi
    done
}

# Local Variables:
# mode: python
# End:
