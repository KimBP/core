# -*- mode:python; -*-

RECIPE_TYPES ?= "canadian-cross"

inherit archive-image
IMAGE_BASENAME ?= "oe-lite-sdk-${HOST_ARCH}-${TARGET_ARCH}"
ARCHIVE_IMAGE_DIRNAME ?= "sdk"
ARCHIVE_IMAGE_FORMATS:HOST_OS_mingw32 = "zip"

IMAGE_PREPROCESS_NETFILTER = ""
IMAGE_PREPROCESS_NETFILTER:HOST_OS_mingw32 = "image_preprocess_linux_netfilter_headers"
IMAGE_PREPROCESS_FUNCS += "${IMAGE_PREPROCESS_NETFILTER}"

image_preprocess_linux_netfilter_headers () {
	oenote image_preprocess_linux_netfilter_headers
	(
	cd ${TARGET_ARCH}/sysroot${target_includedir}/linux
	for f in netfilter*/*.h ; do
		fl=`echo $f | tr '[:upper:]' '[:lower:]'`
		if [ $fl != $f -a -f $fl ] ; then
			mv $f $f-case-conflict
		fi
	done
	)
}

IMAGE_PREPROCESS_FUNCS += "image_rm_sysroot_pkgmetadir"
image_rm_sysroot_pkgmetadir() {
    if [ "${IMAGE_PURGE_PKGMETADIR}" = "1" ] ; then
        rm -rf ${IMAGE_DIR}/${MACHINE_ARCH}/sysroot${pkgmetadir}
    fi
}

CLASS_RDEPENDS += "${RDEPENDS_SDK}"
RDEPENDS_SDK = " \
    gcc \
    target:libc \
    target:libdl \
    target:libpthread \
    target:librt \
    target:libgcc \
    target:libm \
    target:libutil \
    target:libcrypt \
    target:libresolv \
    target:libnss-files \
    target:libnss-dns \
    target:gcc-sysroot-garbage \
    target:gcc-sysroot-locale \
    target:gcc-sysroot-doc \
"
RDEPENDS_SDK:TARGET_LIBC_uclibc = " \
    gcc \
    target:libc \
    target:libdl \
    target:libpthread \
    target:librt \
    target:libgcc \
    target:libm \
    target:libutil \
    target:libcrypt \
    target:libresolv \
    target:gcc-sysroot-garbage \
    target:gcc-sysroot-locale \
    target:gcc-sysroot-doc \
"

CLASS_FLAGS += "sdk_cxx"
DEFAULT_USE_sdk_cxx = "1"
RDEPENDS_SDK += "${RDEPENDS_SDK_CXX}"
RDEPENDS_SDK_CXX = ""
RDEPENDS_SDK_CXX:USE_sdk_cxx = "gcc-g++ target:libstdc++-dev target:libstdc++"

CLASS_FLAGS += "sdk_gdb"
DEFAULT_USE_sdk_gdb = "1"
RDEPENDS_SDK += "${RDEPENDS_SDK_GDB}"
RDEPENDS_SDK_GDB = ""
RDEPENDS_SDK_GDB:USE_sdk_gdb = "gcc-gdb"

CLASS_FLAGS += "sdk_uboot_mkimage"
DEFAULT_USE_sdk_uboot_mkimage = "1"
RDEPENDS_SDK += "${RDEPENDS_SDK_UBOOT_MKIMAGE}"
RDEPENDS_SDK_UBOOT_MKIMAGE = ""
RDEPENDS_SDK_UBOOT_MKIMAGE:USE_sdk_uboot_mkimage = "${UBOOT_MKIMAGE}"
UBOOT_MKIMAGE = "host:u-boot-tools-mkimage"
UBOOT_MKIMAGE:HOST_OS_mingw32 = ""

CLASS_FLAGS += "sdk_archive_formats sdk_archive_tar_ext"
DEFAULT_USE_sdk_archive_formats = "tar"
DEFAULT_USE_sdk_archive_tar_ext = "tar.gz"
ARCHIVE_IMAGE_FORMATS = "${USE_sdk_archive_formats}"
ARCHIVE_IMAGE_DIRNAME = ""
ARCHIVE_IMAGE_TAR_EXT = "${USE_sdk_archive_tar_ext}"

PROVIDES_${PN} = "sdk"
