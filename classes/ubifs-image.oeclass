IMAGE_BASENAME ?= "${PN}-${TARGET_MACHINE_ID}"

inherit image image_mdev image_inetd image_crontab image_makedevs image_inittab image_fstab

UBIFS_IMAGE_DEPENDS = "native:mtd-utils-mkfs-ubifs"
CLASS_DEPENDS += "${UBIFS_IMAGE_DEPENDS}"

IMAGE_CREATE_FUNCS += "ubifs_image"

UBIFS_IMAGE_OPTIONS += "--leb-size=${UBIFS_IMAGE_LEB_SIZE}"
UBIFS_IMAGE_OPTIONS += "--max-leb-cnt=${UBIFS_IMAGE_MAX_LEB_CNT}"
UBIFS_IMAGE_OPTIONS += "--min-io-size=${UBIFS_IMAGE_MIN_IO_SIZE}"

UBIFS_IMAGE_COMPR_TYPE ?= "lzo"
UBIFS_IMAGE_OPTIONS += "--compr=${UBIFS_IMAGE_COMPR_TYPE}"

ubifs_image () {
    mkfs.ubifs ${UBIFS_IMAGE_OPTIONS} \
        --root=${IMAGE_DIR} \
        --output=${B}/${IMAGE_BASENAME}.ubifs
}

DO_INSTALL_POSTFUNCS += "do_install_ubifs_image"
do_install_ubifs_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}.ubifs ${D}/
}

FILES_${PN} += "/*.ubifs"

DO_DEPLOY_POSTFUNCS += "do_deploy_ubifs_image"
do_deploy_ubifs_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}.ubifs \
		${IMAGE_DEPLOY_DIR}/${IMAGE_FULLNAME}.ubifs
	ln -sf ${IMAGE_FULLNAME}.ubifs \
		${IMAGE_DEPLOY_DIR}/${IMAGE_BASENAME}.ubifs
}
