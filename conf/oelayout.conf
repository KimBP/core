#
# Internal OE-core directory layout
#

# Increment this on incompatible changes in any of the variables
# below, thus assuring rebuild is done.  The variables needs to be
# "nohash" as they include full path to TOPDIR
OELAYOUT_ABI	 = 0

DL_DIR		?= "${TOPDIR}/ingredients"
DL_DIR[nohash]	 = True
TMPDIR		 = "${TOPDIR}/tmp"
CACHE		:= "${TMPDIR}/cache${@['', '/' + str(d.getVar('MACHINE', 1))][bool(d.getVar('MACHINE', 1))]}${@['', '/' + str(d.getVar('DISTRO', 1))][bool(d.getVar('DISTRO', 1))]}"
CACHE[nohash]	 = True
SCM_DIR		 = "${TMPDIR}/scm"
SCM_DIR[nohash]	 = True
CVSDIR		 = "${SCM_DIR}/cvs"
CVSDIR[nohash]	 = True
SVNDIR		 = "${SCM_DIR}/svn"
SVNDIR[nohash]	 = True
GITDIR		 = "${SCM_DIR}/git"
GITDIR[nohash]	 = True
BZRDIR		 = "${SCM_DIR}/bzr"
BZRDIR[nohash]	 = True
HGDIR		 = "${SCM_DIR}/hg"
HGDIR[nohash]	 = True

STAMPDIR	 = "${TMPDIR}/stamps/${RECIPE_ARCH}/${PF}"
STAMPDIR[nohash] = True
WORKDIR		 = "${TMPDIR}/work/${RECIPE_ARCH}/${PF}"
WORKDIR[nohash]	 = True
T		 = "${WORKDIR}/tmp"
SRCDIR		 = "${WORKDIR}/src"
S		 = "${SRCDIR}/${BP}"
B		 = "${S}"
D		 = "${WORKDIR}/install"

PKGD		 = "${WORKDIR}/packages"
IMAGE_DIR	 = "${WORKDIR}/image"

STAGE_DIR		 = "${WORKDIR}/stage"
STAGE_UNPACKDIR		 = "${STAGE_DIR}/unpack"
IMAGE_STAGE		 = "${WORKDIR}/rstage"
MACHINE_SYSROOT		 = "${STAGE_DIR}/sysroot"
SDK_SYSROOT		 = "${STAGE_DIR}/sysroot"
TARGET_SYSROOT		 = "${MACHINE_SYSROOT}"
KERNEL_STAGE		 = "${MACHINE_SYSROOT}/kernel"

IMAGE_DEPLOY_DIR	 = "${TMPDIR}/images"
IMAGE_DEPLOY_DIR[nohash] = True
PACKAGE_DEPLOY_DIR 	 = "${TMPDIR}/packages"
PACKAGE_DEPLOY_DIR[nohash] = True

PATH_prepend = "\
${STAGE_DIR}/cross${stage_bindir}:\
${STAGE_DIR}/native${stage_bindir}:\
"

MACHINE_PKG_CONFIG_DIR  = "${MACHINE_SYSROOT}${machine_libdir}/pkgconfig"
MACHINE_PKG_CONFIG_PATH = "${MACHINE_SYSROOT}${machine_datadir}/pkgconfig"
SDK_PKG_CONFIG_DIR = "${SDK_SYSROOT}${sdk_libdir}/pkgconfig"
SDK_PKG_CONFIG_PATH = "${SDK_SYSROOT}${sdk_datadir}/pkgconfig"
export PKG_CONFIG_LIBDIR = "${MACHINE_PKG_CONFIG_DIR}"
export PKG_CONFIG_PATH = "${MACHINE_PKG_CONFIG_PATH}"
export PKG_CONFIG_SYSROOT_DIR = "${TARGET_SYSROOT}"
export PKG_CONFIG_DISABLE_UNINSTALLED = "yes"

# Recipe directory layout
FILE_DIRNAME	= "${@os.path.dirname(d.getVar('FILE', 1))}"
FILESPATHBASE	= "${FILE_DIRNAME}"
FILESPATHPKG	= "${PF}:${P}:${PN}:${BP}:${BPN}:files:."
FILESPATH	= "${@':'.join([os.path.normpath(os.path.join(fp, p, o)) for fp in d.getVar('FILESPATHBASE', 1).split(':') for p in d.getVar('FILESPATHPKG', 1).split(':') for o in (d.getVar('OVERRIDES', 1) + ':').split(':') if os.path.exists(os.path.join(fp, p, o))])}"
FILESDIR	= "${@bb.which(d.getVar('FILESPATH', 1), '.')}"
