# -*- mode:python; -*-
DESCRIPTION = "Distroversion and  build-time file creation."
LICENSE = "N/A"
PACKAGES = "${PN}"

do_install () {
	:
}

do_install[postfuncs] += "${BASE_VERSION_POSTFUNCS}"
BASE_VERSION_POSTFUNCS ?= ""

RECIPE_FLAGS += "basefiles_version"
BASE_VERSION_POSTFUNCS:>USE_basefiles_version = " do_install_basefiles_version"
do_install_basefiles_version[expand] = 3
do_install_basefiles_version () {
	echo "${DISTRO_VERSION}" > \
	      ${SRCDIR}/${USE_basefiles_version}
	install -m 0755 -d ${D}${sysconfdir}
	install -m 0644 ${SRCDIR}/${USE_basefiles_version} \
		${D}${sysconfdir}/${USE_basefiles_version}
}


RECIPE_FLAGS += "basefiles_buildtime"
BASE_VERSION_POSTFUNCS:>USE_basefiles_buildtime = " do_install_basefiles_buildtime"
do_install_basefiles_buildtime[expand] = 3
do_install_basefiles_buildtime () {
	echo "${DATETIME}" > \
		${SRCDIR}/${USE_basefiles_buildtime}

	install -m 0755 -d ${D}${sysconfdir}
	install -m 0644 ${SRCDIR}/${USE_basefiles_buildtime} \
		${D}${sysconfdir}/${USE_basefiles_buildtime}
}

RECIPE_FLAGS += "basefiles_cross_config"
DEPENDS:>USE_basefiles_cross_config = " cross:gcc-config"
BASE_VERSION_POSTFUNCS:>USE_basefiles_cross_config = " do_install_basefiles_cross_config"
do_install_basefiles_cross_config[expand] = 3
do_install_basefiles_cross_config () {
	install -m 0755 -d ${D}${sysconfdir}
        if [ ! -r "${STAGE_DIR}/cross/gcc-config" ]; then
                echo "Error unable to read ${STAGE_DIR}/cross/gcc-config"
                return 1
        fi
	sed -n -e '/NG version/ p' -e 's/#.*//' -e '/PKGVERSION/ d' \
		-e '/VERSION/ p' ${STAGE_DIR}/cross/gcc-config > \
		${SRCDIR}/${USE_basefiles_cross_config}

	install -m 0755 -d ${D}${sysconfdir}
	install -m 0644 ${SRCDIR}/${USE_basefiles_cross_config} \
		${D}${sysconfdir}/${USE_basefiles_cross_config}
}

export BUILD_TAG
RECIPE_FLAGS += "basefiles_buildtag"
BASE_VERSION_POSTFUNCS:>USE_basefiles_buildtag = " do_install_basefiles_buildtag"
DEFAULT_USE_basefiles_buildtag = "build_tag"
do_install_basefiles_buildtag () {
	if [ -n "$BUILD_TAG" ]; then
		echo "$BUILD_TAG" > ${SRCDIR}/${USE_basefiles_buildtag}
		install -m 0755 -d ${D}${sysconfdir}
		install -m 0644 ${SRCDIR}/${USE_basefiles_buildtag} \
			${D}${sysconfdir}/${USE_basefiles_buildtag}
	fi
}
